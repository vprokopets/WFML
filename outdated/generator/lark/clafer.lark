?model: _NL* element+

element: goal | constraint | assertion | clafer 

goal: "<<" ("min" | "max" | "minimize" | "maximize") NAME ">>" _NL
constraint: "[" NAME "]" _NL
assertion: "assert" "[" NAME "]" _NL
clafer: abstract? gcard? name super? reference? fcard? init? _NL [_INDENT element+ _DEDENT]

abstract: "abstract"
gcard: _separated{NUMBER, /\..|,/} | "xor" | "or" | "mux" | "opt"
name: NAME
fcard: _separated{NUMBER, /\..|,/} | "?" | "*" | "+"
super: ":" NAME
reference: "->" NAME | "->>" NAME
init: "=" NAME | ":=" NAME








%import common.WS_INLINE
%import common.CNAME -> NAME
%declare _INDENT _DEDENT
%ignore WS_INLINE
_separated{x, sep}: x (sep x)*
NUMBER : /[0-9]+/
_NL: /(\r?\n[\t ]*)+/
